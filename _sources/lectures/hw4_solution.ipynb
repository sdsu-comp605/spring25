{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "e0d26929",
   "metadata": {},
   "source": [
    "# 33) HW4 Solution\n",
    "\n",
    "## Assignment Steps\n",
    "\n",
    "1. (30%) Write your own MST reduce(to-one) algorithm to sum the rank IDs of all the ranks in the whole communicator. This version should use recursion (as we've seen in class for the MST broadcast)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c4b601fd",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Example implementation of MSTReduce (Fig. 3(b)) from\n",
    "# Chan, E., Heimlich, M., Purkayastha, A. and van de Geijn, R. (2007),\n",
    "# Collective communication: theory, practice, and experience. Concurrency\n",
    "# Computat.: Pract. Exper., 19: 1749â€“1783. doi:10.1002/cpe.1206\n",
    "#\n",
    "# In this minimum spanning tree (mst) reduce algorithm:\n",
    "#   - Divide ranks into two (almost) equal group\n",
    "#   - all ranks in the partition (called srce) reduce data to one rank (root)\n",
    "#   - recurse on two groups with root and srce being the \"roots\" of respective\n",
    "#     groups\n",
    "#\n",
    "\n",
    "function mstreduce!(buf, root, mpicomm;\n",
    "    left = 0, right = MPI.Comm_size(mpicomm)-1)\n",
    "\n",
    "    # If there is no one else, let's get outta here!\n",
    "    left == right && return\n",
    "    # Short hand for:\n",
    "    #=\n",
    "    if left == right\n",
    "    return\n",
    "    end\n",
    "    =#\n",
    "\n",
    "    # Determine the split\n",
    "    mid = div(left + right, 2) # integer division\n",
    "\n",
    "    # Whom do I send to?\n",
    "    srce = (root <= mid) ? right : left\n",
    "    # Short hand for:\n",
    "    #=\n",
    "    if root <= mid\n",
    "    srce = right;\n",
    "    else\n",
    "    srce = left;\n",
    "    end\n",
    "    =#\n",
    "\n",
    "    # Figure out who we are\n",
    "    mpirank = MPI.Comm_rank(mpicomm)\n",
    "\n",
    "    # Recursion:\n",
    "    # I'm in the left group and the root is my new root\n",
    "    if mpirank <= mid && root <= mid\n",
    "        mstreduce!(buf, root, mpicomm; left=left, right=mid)\n",
    "    # I'm in the left group and the srce is my new root\n",
    "    elseif mpirank <= mid && root > mid\n",
    "        mstreduce!(buf, srce, mpicomm; left=left, right=mid)\n",
    "    # I'm in the right group and the srce is my new root\n",
    "    elseif mpirank > mid && root <= mid\n",
    "        mstreduce!(buf, srce, mpicomm; left=mid + 1, right=right)\n",
    "    # I'm in the right group and the root is my new root\n",
    "    elseif mpirank > mid && root > mid\n",
    "        mstreduce!(buf, root, mpicomm; left=mid + 1, right=right)\n",
    "    end\n",
    "\n",
    "    # If I'm the root or srce send or recv (respectively)\n",
    "    req = MPI.REQUEST_NULL\n",
    "    if mpirank == srce\n",
    "        req = MPI.Isend(buf, root, 7, mpicomm)\n",
    "    elseif mpirank == root\n",
    "        # Allocate somewhere to put the data when we get it\n",
    "        recv_buf = similar(buf)\n",
    "        MPI.Recv!(recv_buf, srce, 7, mpicomm)\n",
    "        # update buffer with the received data\n",
    "        buf .= buf .+ recv_buf\n",
    "    end\n",
    "\n",
    "\n",
    "    # Make sure all my sends are done before I get outta dodge\n",
    "    if req != MPI.REQUEST_NULL\n",
    "        MPI.Wait!(req)\n",
    "    end\n",
    "\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a6c5cebf",
   "metadata": {},
   "source": [
    "2. (20%) Extend the testing code `naivereduce_test.jl` to compare the execution times of the naive implementation and your MST implementation (similar to what we did in class to compare the naive and MST broadcasts).\n",
    "\n",
    "I will give the test that compares all methods below in Part 4."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b7618b70",
   "metadata": {},
   "source": [
    "3. (15%) Use the tuckoo cluster to test the execution up to 16 MPI ranks. Use the SLURM batch job script provided in `batch_scripts/batch.jello` as a template and modify it accordingly to launch your program."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ae5aaea8",
   "metadata": {},
   "source": [
    "```{literalinclude} ../julia_codes/hw4_sol/batch.jreduce\n",
    ":language: bash\n",
    ":linenos: true\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0f648976",
   "metadata": {},
   "source": [
    "4. (35%) In the attached Report.ipynb write a commentary and implement your own post-processing/data analysis of your execution results, producing 1 or 2 figures with detailed captions presenting the results of your study."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "58023a50",
   "metadata": {},
   "outputs": [],
   "source": [
    "using MPI\n",
    "include(\"../julia_codes/hw4_sol/naivereduce.jl\")\n",
    "include(\"../julia_codes/hw4_sol/mstreduce.jl\")\n",
    "include(\"../julia_codes/hw4_sol/mstreduce_iter.jl\")\n",
    "\n",
    "let\n",
    "  # Initialize MPI\n",
    "  MPI.Init()\n",
    "\n",
    "  # store communicator\n",
    "  mpicomm = MPI.COMM_WORLD\n",
    "\n",
    "  # Get some MPI info\n",
    "  mpirank = MPI.Comm_rank(mpicomm)\n",
    "  mpisize = MPI.Comm_size(mpicomm)\n",
    "\n",
    "  # Divide all ranks halfway to determine the root\n",
    "  root = div(mpisize, 2) # Integer division\n",
    "\n",
    "  # create buffer for the communication\n",
    "  buf = [mpirank]\n",
    "\n",
    "  # test the different reduce versions (non-recursive MST, recursive MST, and naivereduce)\n",
    "  mstreduce_iter!(buf, root, mpicomm)\n",
    "  mstreduce!(buf, root, mpicomm)\n",
    "  naivereduce!(buf, root, mpicomm)\n",
    "\n",
    "  # Let's time them\n",
    "  mst_iter_t1 = time_ns() # The time_ns() function in Julia returns the current time in nanoseconds\n",
    "  mstreduce_iter!(buf, root, mpicomm)\n",
    "  mst_iter_t2 = time_ns()\n",
    "  mst_t1 = time_ns() # The time_ns() function in Julia returns the current time in nanoseconds\n",
    "  mstreduce!(buf, root, mpicomm)\n",
    "  mst_t2 = time_ns()\n",
    "  nve_t1 = time_ns()\n",
    "  naivereduce!(buf, root, mpicomm)\n",
    "  nve_t2 = time_ns()\n",
    "\n",
    "  mpirank == 0 && print(\"Elapsed time for the naive algorithm: \\n\")\n",
    "  mpirank == 0 && @show (nve_t2 - nve_t1) * 1e-9\n",
    "  mpirank == 0 && print(\"Elapsed time for the recursive mst algorithm: \\n\")\n",
    "  mpirank == 0 && @show (mst_t2 - mst_t1) * 1e-9\n",
    "  mpirank == 0 && print(\"Elapsed time for the iterative (non-recursive) mst algorithm: \\n\")\n",
    "  mpirank == 0 && @show (mst_iter_t2 - mst_iter_t1) * 1e-9\n",
    "\n",
    "  # shutdown MPI\n",
    "  MPI.Finalize()\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b78db3dc",
   "metadata": {},
   "source": [
    "Launching the script from step 3) that executes the `reduce_compare.jl` under different MPI ranks, produces an output file with the following output:\n",
    "\n",
    "```{literalinclude} ../julia_codes/hw4_sol/jred_comp.out\n",
    ":language: bash\n",
    ":linenos: true\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0dc36779",
   "metadata": {},
   "source": [
    "The following post-processing code is used to produce the comparison plot:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5c4ae85c",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Plots\n",
    "default(linewidth=4, legendfontsize=12)\n",
    "using CSV, DataFrames\n",
    "\n",
    "test_results = DataFrame(exec_num = Int[], naive_time = Float64[], mst_time = Float64[], iter_time = Float64[])\n",
    "\n",
    "open(\"../julia_codes/hw4_sol/jred_comp.out\", \"r\")  do f\n",
    "\n",
    "    output_file = read(f, String)\n",
    "\n",
    "    # Regular expressions\n",
    "    # First, split the read file into blocks per Execution\n",
    "    blocks = split(output_file, \"=====================================\")\n",
    "\n",
    "    # Prepare an empty DataFrame\n",
    "    df = DataFrame(execution = Int[], naive_time = Float64[], recursive_mst_time = Float64[], iterative_time = Float64[])\n",
    "\n",
    "    # Loop through each block\n",
    "    for block in blocks\n",
    "        if occursin(r\"Execution \\d+ MPI rank\", block)\n",
    "            # Extract Execution number\n",
    "            exec_match = match(r\"Execution (\\d+)\", block)\n",
    "            exec_num = parse(Int, exec_match.captures[1])\n",
    "\n",
    "            # Extract times\n",
    "            naive_match = match(r\"Elapsed time for the naive algorithm:.*?=\\s*(-?\\d+\\.?\\d*(?:[eE][-+]?\\d+|))\"s, block)\n",
    "            recursive_mst_match = match(r\"Elapsed time for the recursive mst algorithm:.*?=\\s*(-?\\d+\\.?\\d*(?:[eE][-+]?\\d+|))\"s, block)\n",
    "            iterative_match = match(r\"Elapsed time for the iterative \\(non-recursive\\) mst algorithm:.*?=\\s*(-?\\d+\\.?\\d*(?:[eE][-+]?\\d+|))\"s, block)\n",
    "\n",
    "            if naive_match !== nothing && recursive_mst_match !== nothing && iterative_match !== nothing\n",
    "\n",
    "                # Populate if matches found\n",
    "\n",
    "                naive_time = parse(Float64, naive_match.captures[1])\n",
    "                mst_time = parse(Float64, recursive_mst_match.captures[1])\n",
    "                iter_time = parse(Float64, iterative_match.captures[1])\n",
    "\n",
    "                push!(test_results, (exec_num = exec_num, naive_time = naive_time, mst_time = mst_time, iter_time = iter_time))\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "CSV.write(\"test_results.csv\", test_results)\n",
    "\n",
    "pl = plot(\n",
    "    test_results.exec_num, test_results.naive_time,\n",
    "    yscale = :log10,\n",
    "    xlims = (0,16),\n",
    "    xlabel = \"MPI size\",\n",
    "    ylabel = \"Execution time\",\n",
    "    label = \"Naive reduce\",\n",
    "    title = \"Comparison of different parallel MPI reduce(to-one) algorithms\",\n",
    "    marker=:o\n",
    ")\n",
    "\n",
    "plot!(test_results.exec_num, test_results.mst_time, label = \"MST (recursive) reduce\", marker=:diamond)\n",
    "plot!(test_results.exec_num, test_results.iter_time, label = \"Iterative reduce\", marker=:star5)\n",
    "\n",
    "display(pl)\n",
    "savefig(pl, \"comparison_naive_mst_iter_reduce.png\")\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.6",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
